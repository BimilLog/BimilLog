plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

// Load keystore properties from local.properties
def keystorePropertiesFile = rootProject.file("local.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'com.growfarm.bimillog'
    compileSdk 35

    defaultConfig {
        applicationId "com.growfarm.bimillog"
        minSdk 23  // Firebase Crashlytics requires API 23+
        targetSdk 35
        versionCode 7
        versionName "1.1.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('../release.keystore')
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD'] ?: ''
            keyAlias 'bimillog'
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD'] ?: ''
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    // TWA (Trusted Web Activity) 핵심 라이브러리
    // Note: 2.6.x는 androidx.browser 1.9.x (SDK 36 필요)를 사용하므로 2.5.0 유지
    // https://github.com/GoogleChrome/android-browser-helper/issues/538
    implementation 'com.google.androidbrowserhelper:androidbrowserhelper:2.5.0'

    // Firebase BoM (Bill of Materials) - 모든 Firebase 라이브러리 버전 관리
    implementation platform('com.google.firebase:firebase-bom:33.8.0')

    // Firebase Crashlytics - 크래시 리포팅
    implementation 'com.google.firebase:firebase-crashlytics'

    // Firebase Analytics - 사용자 분석
    implementation 'com.google.firebase:firebase-analytics'

    // Android 기본 라이브러리
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.browser:browser:1.7.0'

    // 테스트
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
